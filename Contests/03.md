### Fluctuations in SfrxEth derivative price with Curve's Oracle

**Contest:** [Asymmetry]()

**Context**: [SfrxEth.sol](https://github.com/code-423n4/2023-03-asymmetry/blob/44b5cd94ebedc187a08884a7f685e950e987261c/contracts/SafEth/derivatives/SfrxEth.sol#L111-L117)

**Description:** 

The current issue originates from the use of Curve's Oracle to calculate the price in the SfrxEth derivative, which could lead to fluctuations in the asset's value. Moreover, this can result in potential losses for users when withdrawing funds if the transaction is not executed in the same block. 

The price calculation relies on the amplification factor, pool balances and an exponential moving average. I've prepared a proof of concept test which indicates significant price difference when the transaction spans between multiple blocks. 


**Impact:**

The impact of this issue is that users can potentially lose a portion of their funds when withdrawing, due to fluctuations in the SfrxEth derivative price caused by the use of Curve's Oracle. Although this is a niche scenario, it can still result in significant losses for users under certain circumstances.


**Proof of Concept:**

The ethPerDerivative function is used by SafEth when [staking](https://github.com/code-423n4/2023-03-asymmetry/blob/44b5cd94ebedc187a08884a7f685e950e987261c/contracts/SafEth/SafEth.sol#L63-L100) to define the deposit price and calculate the underlying value.

```solidity
 for (uint i = 0; i < derivativeCount; i++)
      underlyingValue +=
        (derivatives[i].ethPerDerivative(derivatives[i].balance()) *
          derivatives[i].balance()) /
        10 ** 18;

...

      uint derivativeReceivedEthValue = (derivative.ethPerDerivative(
        depositAmount
      ) * depositAmount) / 10 ** 18;

...
```

But in [SfrxEth](https://github.com/code-423n4/2023-03-asymmetry/blob/44b5cd94ebedc187a08884a7f685e950e987261c/contracts/SafEth/derivatives/SfrxEth.sol#L60-L87) it is also used when withdrawing:

```solidity
    function withdraw(uint256 _amount) external onlyOwner {

        IsFrxEth(SFRX_ETH_ADDRESS).redeem(
            _amount,
            address(this),
            address(this)
        );
        uint256 frxEthBalance = IERC20(FRX_ETH_ADDRESS).balanceOf(
            address(this)
        );
        IsFrxEth(FRX_ETH_ADDRESS).approve(
            FRX_ETH_CRV_POOL_ADDRESS,
            frxEthBalance
        );

        uint256 minOut = (((ethPerDerivative(_amount) * _amount) / 10 ** 18) *
            (10 ** 18 - maxSlippage)) / 10 ** 18;
            
        IFrxEthEthPool(FRX_ETH_CRV_POOL_ADDRESS).exchange(
            1,
            0,
            frxEthBalance,
            minOut
        );
        // solhint-disable-next-line
        (bool sent, ) = address(msg.sender).call{value: address(this).balance}(
            ""
        );
        require(sent, "Failed to send Ether");
    }
```

Every derivative has its own ethPerDerivative implementation, [Reth](https://github.com/code-423n4/2023-03-asymmetry/blob/44b5cd94ebedc187a08884a7f685e950e987261c/contracts/SafEth/derivatives/Reth.sol#L211-L215), [WstEth](https://github.com/code-423n4/2023-03-asymmetry/blob/44b5cd94ebedc187a08884a7f685e950e987261c/contracts/SafEth/derivatives/WstEth.sol#L86-L88), [SfrxEth](https://github.com/code-423n4/2023-03-asymmetry/blob/44b5cd94ebedc187a08884a7f685e950e987261c/contracts/SafEth/derivatives/SfrxEth.sol#L111-L117)
However, the issue lies in the calculation of the SfrxEth price:
```solidity
    function ethPerDerivative(uint256 _amount) public view returns (uint256) {
        uint256 frxAmount = IsFrxEth(SFRX_ETH_ADDRESS).convertToAssets(
            10 ** 18
        );
        return ((10 ** 18 * frxAmount) /
            IFrxEthEthPool(FRX_ETH_CRV_POOL_ADDRESS).price_oracle());
    }
```

The function uses a curve pool as a price oracle, specifically the price_oracle function.

The [price_oracle()](https://etherscan.deth.net/address/0xa1F8A6807c402E4A15ef4EBa36528A3FED24E577) function works as follows:

```solidity
def price_oracle() -> uint256:
    amp: uint256 = self._A()
    xp: uint256[N_COINS] = self.balances
    D: uint256 = self._get_D(xp, amp)
    return self._ma_price(xp, amp, D)
```

Get the current value of amp by calling the `_A()` function,where amp represents the amplification factor of the pool, which is a measure of the pool's liquidity.
Fetch the current pool balances of all coins, stored in the balances array.
Calculate the invariant D by calling the` _get_D(xp, amp)` function with the pool balances xp and amplification factor amp. D is the invariant value of the pool, and it is used to maintain the pool's liquidity.
Calculate the price using the `_ma_price(xp, amp, D`) function, which returns the moving average price of the asset. This function internally calls the` _get_p(xp, amp, D) `function to calculate the raw price, and then applies an exponential moving average (EMA) to smooth out the price over time.
The key functions used within price_oracle() are:

`_A()`: This function returns the current amplification factor, taking into account any ongoing ramping up or down.
`_get_D(xp, amp): `This function calculates the invariant D of the pool, which is a measure of the pool's liquidity. It uses an iterative approach to converge to the value of D.
`_get_p(xp, amp, D):` This function calculates the raw price of one asset in terms of another. It utilizes the amplification factor amp, pool balances xp, and invariant D to compute the price.
`_ma_price(xp, amp, D):` This function calculates the moving average price of an asset by applying an exponential moving average (EMA) to the raw price obtained from the` _get_p(xp, amp, D)` function.

In summary, the price_oracle() function calculates the moving average price of one asset in terms of another, considering the current state of the pool, amplification factor, and invariant.

In most cases, the price would be fairly stable, but there are scenarios when depositing and removing liquidity combined by changing EMA can result in fluctations which will be enough for the users to loose funds.

To prove that, I've built a PoC in foundry which shows a price difference of 0,0019105773 between just two blocks.

```solidity
// SPDX-License-Identifier: Unlicense
pragma solidity 0.8.17;
import "forge-std/Test.sol";
import "forge-std/console.sol";
import { ERC20 } from "solmate/tokens/ERC20.sol";

contract CurveOraclePriceTest is Test {
    ICurvePool curvePool = ICurvePool(0xa1F8A6807c402E4A15ef4EBa36528A3FED24E577);


    function setUp() public {
        vm.createSelectFork(
            "https://eth.llamarpc.com",
            15889435
        );
    }

    function testPrice() public {
        console.log(curvePool.price_oracle());
    }

}
```

Running the test with block `15889435` yields the following result:

![](https://i.imgur.com/ZV6o7ej.png)

Re-running it again with block `15889436` shows a significant change:

![](https://i.imgur.com/JAgeHbc.png)


This means that if a user withdraws 3000 eth and 1000 of them go through the Sfrx derivative, this could potentially result to around 1,9105773 loss for the user when withdrawing if the transaction doesn't get executed in the same block.

Finally, this fluctuation actually happens right after [this](https://etherscan.io/tx/0x0edde9c099b76f5a9f2030f798393e8a72b8d692e876f41b437f6baafd582ef2) transaction, which calls add_liquidity for 328 ETH.

![](https://i.imgur.com/gB7Agji.png)


Even though that's quite a niche and a long shot, I'm rating this as a high because there's a real chance that [assets can be partially lost](https://docs.code4rena.com/awarding/judging-criteria/severity-categorization)

**Tools Used**

Manual review, Etherscan, Foundry

**Recommended Mitigation Steps**

If you insist on oracles, it is recommended to move away from Curve's oracle and use Chainlink. Chainlink's oracles are based on VWAP, which considers the volume of the traded asset and provides a more accurate reflection of the true market value over a specific time period. This approach will help reduce the potential for slippage in scenarios like this.

Additionally, you will have out-of-the-box protection for stale prices, invalid data, etc.

My personal recommendation though is to use the frxAmount directly, because the Curve pool calculates the price using its own functionality during the exchange anyway.